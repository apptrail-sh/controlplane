plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.2.0'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.2.0'
    id 'org.springframework.boot' version '4.0.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.jetbrains.kotlin.plugin.jpa' version '2.2.0'
    id 'com.google.cloud.tools.jib' version '3.5.2'
}

group = 'sh.apptrail'
version = '0.0.1-SNAPSHOT'
description = 'controlplane'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2025.1.1")
    set('testcontainersVersion', "2.0.3")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    implementation 'org.springframework.boot:spring-boot-starter-jetty'
    implementation 'tools.jackson.module:jackson-module-kotlin'

    // JWT for GitHub App authentication
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    // BouncyCastle for PEM key parsing (supports PKCS#1 and PKCS#8, Java 8-25+)
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.83'

    // Native Google Cloud Pub/Sub (spring-cloud-gcp-starter-pubsub is not compatible with Spring Boot v4 yet)
    implementation 'com.google.cloud:google-cloud-pubsub:1.147.0'

    implementation 'org.springframework.boot:spring-boot-starter-flyway'
    implementation 'org.flywaydb:flyway-database-postgresql'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:5.4.0'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:testcontainers-postgresql'
    testImplementation 'org.testcontainers:testcontainers-junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll '-Xjsr305=strict'
    }
}

allOpen {
    annotation 'jakarta.persistence.Entity'
    annotation 'jakarta.persistence.MappedSuperclass'
    annotation 'jakarta.persistence.Embeddable'
}

tasks.named('test') {
    useJUnitPlatform()
}

jib {
    from {
        image = 'bellsoft/liberica-openjre-alpine:21'
    }
    to {
        image = 'ghcr.io/apptrail-sh/controlplane'
        tags = (project.findProperty('imageTag') ?: 'latest').toString().split(',').toList()
    }
    container {
        ports = ['3000']
        user = '65532:65532'
        environment = [
            'JAVA_OPTS': '-XX:+UseContainerSupport -XX:MaxRAMPercentage=75.0'
        ]
        creationTime = 'USE_CURRENT_TIMESTAMP'
    }
}
